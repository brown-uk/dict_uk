apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = '3.7.1'
group = 'ua.net.nlp'

String artifactId = 'morfologik-ukrainian'
archivesBaseName = artifactId

String ltVersion = '3.7-SNAPSHOT'

String langCode="uk"

String packageDir="ua/net/nlp"
String resourceDir=sourceSets.main.resources.srcDirs[0].toString() + "/" + packageDir
String outResDir=sourceSets.main.output.resourcesDir.toString() + "/" + packageDir
String tmpDir="build/tmp"


String inputDir="${projectDir}/../../out"
String inputDictFile="dict_corp_lt.txt"

String dstEncoding="cp1251"
String srcEncoding="utf-8"



repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
  provided {
        description = 'Configuration for generating the dictionaries'
  }
}

dependencies {
    provided 'org.languagetool:languagetool-tools:' + ltVersion
}


task prepareDict() {
    def srcDict="${inputDir}/$inputDictFile"
    def outFile="${tmpDir}/all.tagged.nlp.tmp"
    def splitter = '\t'
    def tagSplitter = '|'

    inputs.file srcDict
    outputs.file outFile

    doLast {
        println "Preparing dict file $inputDictFile"
        new File("$projectDir/$tmpDir").mkdirs()

        def outF = new File(outFile)
        outF.text = ''

        // to lower memory footprint we collect by lemmas

        def prevLemma = ""
        def tagMap = [:].withDefault{[]}

        new File(srcDict).eachLine { line ->
          def (word, lemma, tagStr) = line.split()

          def wordKey = word + splitter + lemma
          if( lemma != prevLemma ) {
            tagMap.each { key, tags ->
              def outLine = key + splitter + tags.join(tagSplitter) + "\n"
              outF.append(outLine, dstEncoding)
            }
            tagMap.clear()

            prevLemma = lemma
          }

          tagMap[wordKey]  << tagStr
        }

        tagMap.each { key, tags ->
            def outLine = key + splitter + tags.join(tagSplitter) + "\n"
            outF.append(outLine, dstEncoding)
        }
    }

}


task posDict(type: JavaExec, dependsOn: prepareDict) {
    def outputDict="${outResDir}/ukrainian.dict"

    inputs.file tasks.prepareDict.outputs
    outputs.file outputDict

    workingDir "$projectDir"

    classpath = files(configurations.provided.files)
    main = 'org.languagetool.tools.POSDictionaryBuilder'

    args "-i", tasks.prepareDict.outputs.getFiles().iterator().next()
    args "-info", "${resourceDir}/ukrainian.info"
    args "-o", "${outputDict}"
    
    doFirst {
        new File(outputDict).mkdirs()
    }
}


task copyExtraFiles(type: Copy) {
  from("$projectDir/../../doc/tags.txt")
  into (sourceSets.main.output.resourcesDir.absolutePath + '/ua/net/nlp/')
  rename("tags.txt", "tagset.txt")
}


jar {
    manifest {
        attributes 'Implementation-Title': 'Ukrainian part-of-speech (POS) dictionary as a Morfologik binary',
                   'Implementation-Version': version
    }

    setArchiveName "${artifactId}-${version}.jar"

//    from(sourceSets.main.output) {
        include '**/ua/net/nlp/*'
//    }

    includeEmptyDirs false
}


processResources.dependsOn(copyExtraFiles)


//classes.dependsOn(createPOSDict, createSynthDict, createTagList, createSpellDict, createCaseGovernmentDict)
compileJava.enabled = false

signing {
    sign configurations.archives
}



publishing {
    repositories {
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            //artifact dictJar
            //artifact ltJar
        }
    }
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Ukrainian POS tag dictionary for Morfologik'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Ukrainian part-of-speech dictionaries in Morfologik binary format'
        url 'https://github.com/brown-uk/dict_uk'

        scm {
          url 'https://github.com/brown-uk/dict_uk.git'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License'
            url 'http://www.gnu.org/licenses/lgpl.txt'
          }
          license {
            name 'Creative Commons Attribution-ShareAlike 4.0 International License (CC BY-SA 4.0)'
            url 'http://creativecommons.org/licenses/by-nc-sa/4.0'
          }
        }

        developers {
          developer {
            id 'arysin'
            name 'Andriy Rysin'
            email 'arysin@gmail.com'
          }
        }
      }

    }
  }
}
